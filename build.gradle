import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:7.3.2'
    }
}

plugins {
    id 'org.jetbrains.changelog'
}

apply plugin: 'org.jetbrains.changelog'

changelog {
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()} - ${new SimpleDateFormat('yyyy-MM-dd').format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+.+)/
    itemPrefix = '-'
    keepUnreleasedSection = true
    unreleasedTerm = 'Unreleased work'
    groups = ['Features', 'Fixed', 'Security']
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'

    group 'dk.sunepoulsen.tes'
    version "${project.property('version')}"


    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url "${System.properties['maven.repository.base.url']}/${System.properties['maven.repository.snapshots']}"
            allowInsecureProtocol = true
        }
        maven {
            url "${System.properties['maven.repository.base.url']}/${System.properties['maven.repository.releases']}"
            allowInsecureProtocol = true
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    task allWrappers(type: Wrapper) {
        gradleVersion = "${project.property('gradle.version')}"
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        // Logging
        implementation 'ch.qos.logback:logback-classic:1.2.11'
        implementation 'org.slf4j:log4j-over-slf4j:1.7.36'

        // Spock
        testImplementation 'org.codehaus.groovy:groovy-all:3.0.13'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
        testImplementation 'net.bytebuddy:byte-buddy:1.12.18'
        testImplementation 'org.objenesis:objenesis:3.2'
    }

    test {
        useJUnitPlatform()
    }
}
